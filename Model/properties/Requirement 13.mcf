% If a plane wants to land but the controller would not be able to safely
% handle the plane given the current state of the system, the controller shall
% tell the plane to hold.
forall P:Plane .
nu Y(runwayBusy1: Bool = false, taxiwayBusy1: Bool = false, 
gate1Busy1: Bool = false, gate2Busy1: Bool = false) . (
   [!(
      (exists P1:Plane, L1:Location . assign(L1, P1) || unassign(L1, P1))
   )] Y(runwayBusy1, taxiwayBusy1, gate1Busy1, gate2Busy1)
   && forall P2 : Plane . (
      [assign(runway, P2)]Y(true, taxiwayBusy1, gate1Busy1, gate2Busy1)
      && [assign(taxiway, P2)]Y(runwayBusy1, true, gate1Busy1, gate2Busy1)
      && [assign(gate1, P2)]Y(runwayBusy1, taxiwayBusy1, true, gate2Busy1)
      && [assign(gate2, P2)]Y(runwayBusy1, taxiwayBusy1, gate1Busy1, true)
      && [unassign(runway, P2)]Y(false, taxiwayBusy1, gate1Busy1, gate2Busy1)
      && [unassign(taxiway, P2)]Y(runwayBusy1, false, gate1Busy1, gate2Busy1)
      && [unassign(gate1, P2)]Y(runwayBusy1, taxiwayBusy1, false, gate2Busy1)
      && [unassign(gate2, P2)]Y(runwayBusy1, taxiwayBusy1, gate1Busy1, false)
   )
   && [receiveRequest(P, leave)]
   ((val(runwayBusy1 || taxiwayBusy1)) => (
      mu X. [!sendResponse(P, hold)]X && <true>true
   ))
)